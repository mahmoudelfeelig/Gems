plugins {
    id "fabric-loom" version "1.11-SNAPSHOT"
    id "maven-publish"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
}

loom {
    splitEnvironmentSourceSets()
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    add("modClientImplementation", "net.fabricmc.fabric-api:fabric-api:${fabric_version}")

    testImplementation platform("org.junit:junit-bom:5.10.2")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
    if (project.hasProperty("lintDeprecation")) {
        it.options.compilerArgs.addAll(["-Xlint:deprecation"])
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    def licenseFile = file("LICENSE")
    if (licenseFile.exists()) {
        from(licenseFile) {
            rename { "${it}_${base.archivesName.get()}" }
        }
    }
}
